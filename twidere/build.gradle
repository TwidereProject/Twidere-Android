import fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask

import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: "androidsvgdrawable"
apply from: rootProject.file('global.gradle')
apply from: rootProject.file('signing.gradle')

android {

    defaultConfig {
        applicationId "org.mariotaku.twidere"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 128
        versionName "0.3.0"
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
        google {
        }
        fdroid {
        }
    }
    buildTypes {
        debug {
            SimpleDateFormat format = new SimpleDateFormat("yyyy-MM-dd HH:mm")
            versionNameSuffix String.format(" (dev %s)", format.format(new Date()))
        }
        release {
            SimpleDateFormat format = new SimpleDateFormat("yyyy-MM-dd")
            versionNameSuffix String.format(" (dev %s)", format.format(new Date()))
        }
    }
    sourceSets {
        main {
            res.srcDirs = [project.file("src/$name/res"), project.file("src/$name/res-localized"),
                           project.file("src/$name/res-svg2png")]
        }
    }
}

repositories {
    maven { url 'https://s3.amazonaws.com/repo.commonsware.com' }
}

configurations {
    all*.exclude group: 'org.yaml', module: 'snakeyaml'
}

dependencies {
//    wearApp project(':twidere.wear')
    apt 'com.bluelinelabs:logansquare-compiler:1.1.0'
    apt 'com.hannesdorfmann.parcelableplease:processor:1.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:support-v13:23.0.1'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:cardview-v7:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.twitter:twitter-text:1.12.1'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'
    compile 'com.squareup:otto:1.3.8'
    compile 'dnsjava:dnsjava:2.1.7'
    compile 'com.commonsware.cwac:merge:1.1.1'
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.4.0'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.7'
    compile 'com.github.johnpersano:supertoasts:1.3.4.1@aar'
    compile 'com.github.mariotaku:MessageBubbleView:1.2'
    compile 'com.github.mariotaku:DragSortListView:0.6.1'
    compile 'com.github.uucky:ColorPicker-Android:0.9.7@aar'
    compile 'com.github.boxme:asyncmanager:1.0.0'
    compile 'com.sprylab.android.texturevideoview:texturevideoview:1.0.1'
    compile 'com.squareup:pollexor:2.0.4'
    compile 'com.squareup:tape:1.2.3'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-primitives:commons-primitives:1.0'
    compile 'com.bluelinelabs:logansquare:1.1.0'
    compile 'ch.acra:acra:4.6.2'
    compile 'org.jraf:android-switch-backport:2.0.1'
    compile 'com.fasterxml.jackson.jr:jackson-jr-objects:2.6.2'
    compile 'com.makeramen:roundedimageview:2.1.1'
    compile 'com.soundcloud.android:android-crop:1.0.1@aar'
    compile 'com.hannesdorfmann.parcelableplease:annotation:1.0.1'
    compile 'com.github.mariotaku:PickNCrop:44b09cbc69'
    compile 'com.diogobernardino:williamchart:2.0.1'
    compile 'com.lnikkila:extendedtouchview:0.1.0'
    compile 'com.google.dagger:dagger:2.0.1'
    googleCompile 'com.google.android.gms:play-services-maps:8.1.0'
    googleCompile 'com.google.maps.android:android-maps-utils:0.4'
    fdroidCompile 'org.osmdroid:osmdroid-android:4.3'
    fdroidCompile 'org.slf4j:slf4j-simple:1.7.12'
    debugCompile 'com.facebook.stetho:stetho:1.2.0'
    debugCompile 'com.facebook.stetho:stetho-okhttp:1.2.0'
    compile project(':twidere.component.common')
    compile project(':twidere.component.nyan')
    compile fileTree(dir: 'libs/main', include: ['*.jar'])
    provided 'javax.annotation:jsr250-api:1.0'
//    googleCompile fileTree(dir: 'libs/google', include: ['*.jar'])
}

task svgToDrawable(type: SvgDrawableTask) {
    // specify where to pick SVG from
    from = file('src/main/svg/drawable')
    // specify the android res folder
    to = file('src/main/res-svg2png')
    // create qualified directories if missing
    createMissingDirectories = true
    // override files only if necessary
    overrideMode = 'ifModified'
    // let generate PNG for the following densities only
    targetedDensities = ['hdpi', 'mdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    // relative path of the file specifying nine patch specs
    ninePatchConfig = file('src/main/svg/drawable/9patch.json')
    // output format of the generated resources
    outputFormat = 'PNG'

    outputType = 'drawable'
}