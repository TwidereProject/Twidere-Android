import fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'androidsvgdrawable'

// START Non-FOSS component
apply plugin: 'io.fabric'
// END Non-FOSS component

buildscript {
    repositories {
        // START Non-FOSS component
        maven { url 'https://maven.fabric.io/public' }
        // END Non-FOSS component
    }

    dependencies {
        // START Non-FOSS component
        classpath 'io.fabric.tools:gradle:1.21.5'
        // END Non-FOSS component
    }
}

android {

    defaultConfig {
        applicationId "com.emojidex.twidere"
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 3
        versionName "1.0.3"
        multiDexEnabled true

        buildConfigField 'boolean', 'LEAK_CANARY_ENABLED', 'Boolean.parseBoolean("false")'
        buildConfigField 'boolean', 'SHOW_CUSTOM_TOKEN_DIALOG', 'Boolean.parseBoolean("false")'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    productFlavors {
        // START Non-FOSS component
        google {}
        // END Non-FOSS component
        fdroid {}
    }
    buildTypes {
        debug {
            resValue("bool", "debug", "true")
        }
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue("bool", "debug", "false")
        }
    }
    sourceSets {
        main {
            res.srcDirs += project.files("src/$name/res-localized")
            res.srcDirs += project.files("src/$name/res-svg2png")
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

}

repositories {
    maven { url 'https://s3.amazonaws.com/repo.commonsware.com' }
    // START Non-FOSS component
    maven { url 'https://maven.fabric.io/public' }
    // END Non-FOSS component
    flatDir { dirs "$projectDir/lib" }

    maven { url 'https://raw.github.com/emojidex/emojidex-android/master/EmojidexAndroidLibrary/repository' }
}

dependencies {
//    wearApp project(':twidere.wear')
    apt 'com.bluelinelabs:logansquare-compiler:1.3.7'
    apt 'com.hannesdorfmann.parcelableplease:processor:1.0.2'
    apt 'com.google.dagger:dagger-compiler:2.1'
    apt 'com.github.mariotaku.ObjectCursor:processor:0.9.9'

    androidTestApt 'com.bluelinelabs:logansquare-compiler:1.3.7'

    compile('com.github.mariotaku:app-theme-engine:1efc6237e1@aar') {
        transitive = true
    }


    compile project(':twidere.component.common')
    compile project(':twidere.component.nyan')

    // START Non-FOSS component
    googleCompile 'com.google.android.gms:play-services-maps:9.0.2'
    googleCompile 'com.google.android.gms:play-services-auth:9.0.2'
    googleCompile 'com.google.maps.android:android-maps-utils:0.4.3'
    googleCompile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') { transitive = true }
    googleCompile ':YouTubeAndroidPlayerApi:1.2.2@jar'
    // END Non-FOSS component

    fdroidCompile 'org.osmdroid:osmdroid-android:5.1'

    debugCompile 'com.facebook.stetho:stetho:1.3.1'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.3.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'

    provided 'javax.annotation:jsr250-api:1.0'

    testCompile 'junit:junit:4.12'

    androidTestCompile 'com.android.support:support-annotations:24.0.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'

    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:support-v13:24.0.0'
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:cardview-v7:24.0.0'
    compile 'com.android.support:recyclerview-v7:24.0.0'
    compile 'com.android.support:preference-v7:24.0.0'
    compile 'com.android.support:preference-v14:24.0.0'
    compile 'com.twitter:twitter-text:1.13.0'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.5.0'
    compile 'com.squareup:otto:1.3.8'
    compile 'dnsjava:dnsjava:2.1.7'
    compile 'com.commonsware.cwac:merge:1.1.1'
    compile 'com.commonsware.cwac:layouts:0.4.2'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.github.mariotaku:MessageBubbleView:1.2'
    compile 'com.github.mariotaku:DragSortListView:0.6.1'
    compile 'com.github.uucky:ColorPicker-Android:0.9.7@aar'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.15'
    compile 'com.sprylab.android.texturevideoview:texturevideoview:1.1.1'
    compile 'com.squareup:pollexor:2.0.4'
    compile 'com.squareup:tape:1.2.3'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-primitives:commons-primitives:1.0'
    compile 'com.bluelinelabs:logansquare:1.3.7'
    compile 'com.soundcloud.android:android-crop:1.0.1@aar'
    compile 'com.hannesdorfmann.parcelableplease:annotation:1.0.2'
    compile 'com.github.mariotaku:PickNCrop:0.9.4'
    compile 'com.github.mariotaku.RestFu:library:0.9.29'
    compile 'com.github.mariotaku.RestFu:okhttp3:0.9.29'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.lnikkila:extendedtouchview:0.1.0'
    compile 'com.google.dagger:dagger:2.1'
    compile 'org.attoparser:attoparser:1.4.0.RELEASE'
    compile 'com.github.mariotaku.MediaViewerLibrary:base:0.9.17'
    compile 'com.github.mariotaku.MediaViewerLibrary:subsample-image-view:0.9.17'
    compile 'com.github.mariotaku.SQLiteQB:library:0.9.6'
    compile 'com.github.mariotaku.ObjectCursor:core:0.9.9'
    compile 'com.github.mariotaku:MultiValueSwitch:0.9.6'
    compile 'com.github.mariotaku:AbstractTask:0.9.2'
    compile 'com.github.mariotaku.CommonsLibrary:parcel:0.9.8'
    compile 'com.github.mariotaku.CommonsLibrary:io:0.9.8'

    compile 'org.genshin:emojidexandroid-lib:0.0.5'
}

task svgToDrawable(type: SvgDrawableTask) {
    // specify where to pick SVG from
    from = files('src/main/svg/drawable')
    // specify the android res folder
    to = file('src/main/res-svg2png')
    // override files only if necessary
    overwriteMode = 'ifModified'
    // let generate PNG for the following densities only
    targetedDensities = ['hdpi', 'mdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    // relative path of the file specifying nine patch specs
    ninePatchConfig = file('src/main/svg/drawable/9patch.json')
    // output format of the generated resources
    outputFormat = 'PNG'

    outputType = 'drawable'
}

task svgToMipmap(type: SvgDrawableTask) {
    // specify where to pick SVG from
    from = files('src/main/svg/mipmap')
    // specify the android res folder
    to = file('src/main/res-svg2png')
    // override files only if necessary
    overwriteMode = 'ifModified'
    // let generate PNG for the following densities only
    targetedDensities = ['hdpi', 'mdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    // output format of the generated resources
    outputFormat = 'PNG'

    outputType = 'mipmap'
}

// START Non-FOSS component
//apply plugin: 'com.google.gms.google-services'
// END Non-FOSS component