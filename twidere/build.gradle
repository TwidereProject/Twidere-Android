import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply from: rootProject.file('global.gradle')
apply from: rootProject.file('signing.gradle')

android {

    defaultConfig {
        applicationId "org.mariotaku.twidere"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 107
        versionName "0.3.0"
        multiDexEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
    productFlavors {
        google {
        }
        fdroid {
        }
    }
    buildTypes {
        debug {
            SimpleDateFormat format = new SimpleDateFormat("yyyy-MM-dd HH:mm")
            versionNameSuffix String.format(" (dev %s)", format.format(new Date()))
        }
        release {
            SimpleDateFormat format = new SimpleDateFormat("yyyy-MM-dd")
            versionNameSuffix String.format(" (dev %s)", format.format(new Date()))
        }
    }
    sourceSets {
        main {
            res.srcDirs = [project.file("src/$name/res"), project.file("src/$name/res-localized")]
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://repo.commonsware.com.s3.amazonaws.com' }
    maven { url 'https://github.com/suckgamony/RapidDecoder/raw/master/repository' }
    maven { url "https://jitpack.io" }
}

configurations.all {
    exclude group: 'org.yaml', module: 'snakeyaml'
}

dependencies {
//    wearApp project(':twidere.wear')
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:support-v13:22.0.0'
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:cardview-v7:22.0.0'
    compile 'com.android.support:recyclerview-v7:22.0.0'
    compile 'com.sothree.slidinguppanel:library:3.0.0'
    compile 'com.twitter:twitter-text:1.11.1'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
    compile 'com.squareup:otto:1.3.6'
    compile 'dnsjava:dnsjava:2.1.7'
    compile 'com.commonsware.cwac:merge:1.1.1'
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.1.3'
    compile 'com.rengwuxian.materialedittext:library:2.0.3'
    compile 'com.pnikosis:materialish-progress:1.5'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    // Disabled temporarilly due to attribute clash with subsampling-scale-image-view
//    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.3'
    compile 'com.github.mariotaku:MessageBubbleView:1.0'
    compile 'com.github.mariotaku:DragSortListView:0.6.1'
    compile 'com.github.mariotaku:SlidingMenu:1.3'
    compile 'com.github.uucky:ColorPicker-Android:0.9.1'
    compile 'com.sprylab.android.texturevideoview:texturevideoview:1.0.0'
    compile 'com.squareup:pollexor:2.0.2'
    compile 'org.apache.commons:commons-lang3:3.4'
    googleCompile 'com.google.android.gms:play-services-maps:7.0.0'
    googleCompile 'com.google.maps.android:android-maps-utils:0.3.4'
    fdroidCompile 'org.osmdroid:osmdroid-android:4.3'
    fdroidCompile 'org.slf4j:slf4j-simple:1.7.12'
    debugCompile 'im.dino:dbinspector:3.1.0@aar'
    compile project(':twidere.component.common')
    compile project(':twidere.component.nyan')
    compile fileTree(dir: 'libs/main', include: ['*.jar'])
//    googleCompile fileTree(dir: 'libs/google', include: ['*.jar'])
}