import fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask

import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'androidsvgdrawable'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.2'
    }
}

android {

    defaultConfig {
        applicationId "org.mariotaku.twidere"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 136
        versionName "0.3.0"
        multiDexEnabled true

        buildConfigField 'boolean', 'ENABLE_MEDIA_VIEWER', 'Boolean.parseBoolean("true")'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
        google {}
        fdroid {}
    }
    buildTypes {
        debug {
            SimpleDateFormat format = new SimpleDateFormat("yyyy-MM-dd")
            versionNameSuffix String.format(" (debug %s)", format.format(new Date()))
            resValue("bool", "debug", "true")
        }
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            SimpleDateFormat format = new SimpleDateFormat("yyyy-MM-dd")
            versionNameSuffix String.format(" (%s)", format.format(new Date()))
            resValue("bool", "debug", "false")
        }
    }
    sourceSets {
        main {
            res.srcDirs = [project.file("src/$name/res"), project.file("src/$name/res-localized"),
                           project.file("src/$name/res-svg2png")]
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

repositories {
    maven { url 'https://s3.amazonaws.com/repo.commonsware.com' }
    maven { url 'https://maven.fabric.io/public' }
    flatDir { dirs "$projectDir/lib" }
}

dependencies {
//    wearApp project(':twidere.wear')
    apt 'com.bluelinelabs:logansquare-compiler:1.3.4'
    apt 'com.hannesdorfmann.parcelableplease:processor:1.0.2'
    apt 'com.google.dagger:dagger-compiler:2.0.2'
    apt "com.github.mariotaku.ObjectCursor:processor:0.9.3"
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:support-v13:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.twitter:twitter-text:1.13.0'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'com.squareup:otto:1.3.8'
    compile 'dnsjava:dnsjava:2.1.7'
    compile 'com.commonsware.cwac:merge:1.1.1'
    compile 'com.commonsware.cwac:layouts:0.4.2'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.squareup.okhttp3:okhttp:3.1.0'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.7'
    compile 'com.github.johnpersano:supertoasts:1.3.4.1@aar'
    compile 'com.github.mariotaku:MessageBubbleView:1.2'
    compile 'com.github.mariotaku:DragSortListView:0.6.1'
    compile 'com.github.uucky:ColorPicker-Android:0.9.7@aar'
    compile 'com.github.boxme:asyncmanager:1.0.0'
    compile 'com.sprylab.android.texturevideoview:texturevideoview:1.1.1'
    compile 'com.squareup:pollexor:2.0.4'
    compile 'com.squareup:tape:1.2.3'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-primitives:commons-primitives:1.0'
    compile 'com.bluelinelabs:logansquare:1.3.4'
    compile 'org.jraf:android-switch-backport:2.0.1'
    compile 'com.github.mariotaku:jackson-jr-trees:7fe682ee09'
    compile 'com.makeramen:roundedimageview:2.1.1'
    compile 'com.soundcloud.android:android-crop:1.0.1@aar'
    compile 'com.hannesdorfmann.parcelableplease:annotation:1.0.2'
    compile 'com.github.mariotaku:PickNCrop:0.9.3'
    compile 'com.github.mariotaku.RestFu:library:0.9.17'
    compile 'com.github.mariotaku.RestFu:okhttp3:0.9.17'
    compile 'com.github.mariotaku:InetAddressJni:0.9.1'
    compile 'com.squareup.okhttp3:okhttp:3.1.0'
    compile 'com.lnikkila:extendedtouchview:0.1.0'
    compile 'com.google.dagger:dagger:2.0.2'
    compile 'org.attoparser:attoparser:1.4.0.RELEASE'
    compile 'com.j256.simplemagic:simplemagic:1.6'
    compile 'com.github.mariotaku.MediaViewerLibrary:base:0.9.7'
    compile 'com.github.mariotaku.MediaViewerLibrary:subsample-image-view:0.9.7'
    googleCompile 'com.google.android.gms:play-services-maps:8.4.0'
//    googleCompile 'com.google.maps.android:android-maps-utils:0.4'
    googleCompile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') { transitive = true }
    googleCompile ':YouTubeAndroidPlayerApi:1.2.2@jar'
    fdroidCompile 'org.osmdroid:osmdroid-android:5.1'
    debugCompile 'com.facebook.stetho:stetho:1.3.0'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.3.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta1'
    compile project(':twidere.component.common')
    compile project(':twidere.component.nyan')
    compile 'com.github.mariotaku.ObjectCursor:core:0.9.3'
    compile fileTree(dir: 'libs/main', include: ['*.jar'])
    provided 'javax.annotation:jsr250-api:1.0'

    testCompile 'junit:junit:4.12'

    androidTestCompile 'com.android.support:support-annotations:23.1.1'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
}

task svgToDrawable(type: SvgDrawableTask) {
    // specify where to pick SVG from
    from = files('src/main/svg/drawable')
    // specify the android res folder
    to = file('src/main/res-svg2png')
    // override files only if necessary
    overwriteMode = 'ifModified'
    // let generate PNG for the following densities only
    targetedDensities = ['hdpi', 'mdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    // relative path of the file specifying nine patch specs
    ninePatchConfig = file('src/main/svg/drawable/9patch.json')
    // output format of the generated resources
    outputFormat = 'PNG'

    outputType = 'drawable'
}

task svgToMipmap(type: SvgDrawableTask) {
    // specify where to pick SVG from
    from = files('src/main/svg/mipmap')
    // specify the android res folder
    to = file('src/main/res-svg2png')
    // override files only if necessary
    overwriteMode = 'ifModified'
    // let generate PNG for the following densities only
    targetedDensities = ['hdpi', 'mdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    // output format of the generated resources
    outputFormat = 'PNG'

    outputType = 'mipmap'
}