apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1'

    defaultConfig {
        applicationId "org.mariotaku.twidere"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 98
        versionName "0.3.0-dev"

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        debug {
            File signingPropFile = project.rootProject.file('signing.properties')
            if (signingPropFile.exists()) {
                Properties signingProp = new Properties()
                signingProp.load(signingPropFile.newDataInputStream())
                storeFile file(signingProp.get("twidere.debug.storeFile"))
                storePassword signingProp.get("twidere.debug.storePassword")
                keyAlias signingProp.get("twidere.debug.keyAlias")
                keyPassword signingProp.get("twidere.debug.keyPassword")
            }
        }
        release {
            File signingPropFile = project.rootProject.file('signing.properties')
            if (signingPropFile.exists()) {
                Properties signingProp = new Properties()
                signingProp.load(signingPropFile.newDataInputStream())
                storeFile file(signingProp.get("twidere.release.storeFile"))
                storePassword signingProp.get("twidere.release.storePassword")
                keyAlias signingProp.get("twidere.release.keyAlias")
                keyPassword signingProp.get("twidere.release.keyPassword")
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
    buildTypes {
        debug {
            File signingPropFile = project.rootProject.file('signing.properties')
            if (signingPropFile.exists()) {
                signingConfig signingConfigs.debug
            }
        }
        release {
            File signingPropFile = project.rootProject.file('signing.properties')
            if (signingPropFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }
    lintOptions {
        abortOnError false
    }
    productFlavors {
        google {
            versionName = android.defaultConfig.versionName + '-google'
        }
        fdroid {
            versionName = android.defaultConfig.versionName + '-fdroid'
        }
    }
}

dependencies {
//    wearApp project(':twidere.wear')
    compile 'com.android.support:support-v13:21.0.0'
    compile 'com.android.support:cardview-v7:21.0.0'
    compile 'com.android.support:recyclerview-v7:21.0.0'
    compile 'com.android.support:palette-v7:21.0.0'
    compile 'com.etsy.android.grid:library:1.0.5'
    compile 'com.sothree.slidinguppanel:library:2.0.0'
    compile 'it.sephiroth.android.library.imagezoom:imagezoom:1.0.5'
    compile 'com.twitter:twitter-text:1.9.9'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
    compile 'org.apache.httpcomponents:httpclient-android:4.3.5'
    compile 'org.apache.httpcomponents:httpmime:4.3.5'
    compile 'ch.acra:acra:4.5.0'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    compile 'me.grantland:autofittextview:0.2.0'
    compile 'com.github.johnpersano:supertoasts:1.3.4.1@aar'
    googleCompile 'com.google.android.gms:play-services:6.1.71'
    fdroidCompile 'org.osmdroid:osmdroid-android:4.2'
    fdroidCompile 'org.slf4j:slf4j-simple:1.6.1'
    compile project(':SlidingMenu')
    compile project(':DragSortListView')
    compile project(':MenuComponent')
    compile project(':RefreshNow')
    compile project(':MessageBubbleView')
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
