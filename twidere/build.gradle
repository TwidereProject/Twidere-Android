apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId "org.mariotaku.twidere"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 99
        versionName "0.3.0-dev"

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
    signingConfigs {
        debug {
            File signingPropFile = project.rootProject.file('signing.properties')
            if (signingPropFile.exists()) {
                Properties signingProp = new Properties()
                signingProp.load(signingPropFile.newDataInputStream())
                storeFile file(signingProp.get("twidere.debug.storeFile"))
                storePassword signingProp.get("twidere.debug.storePassword")
                keyAlias signingProp.get("twidere.debug.keyAlias")
                keyPassword signingProp.get("twidere.debug.keyPassword")
            }
        }
        release {
            File signingPropFile = project.rootProject.file('signing.properties')
            if (signingPropFile.exists()) {
                Properties signingProp = new Properties()
                signingProp.load(signingPropFile.newDataInputStream())
                storeFile file(signingProp.get("twidere.release.storeFile"))
                storePassword signingProp.get("twidere.release.storePassword")
                keyAlias signingProp.get("twidere.release.keyAlias")
                keyPassword signingProp.get("twidere.release.keyPassword")
            }
        }
    }
    buildTypes {
        debug {
            File signingPropFile = project.rootProject.file('signing.properties')
            if (signingPropFile.exists()) {
                signingConfig signingConfigs.debug
            }
        }
        release {
            File signingPropFile = project.rootProject.file('signing.properties')
            if (signingPropFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    productFlavors {
        google {
            versionName = android.defaultConfig.versionName + '-google'
        }
        fdroid {
            versionName = android.defaultConfig.versionName + '-fdroid'
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://repo.commonsware.com.s3.amazonaws.com' }
}

configurations.all {
    exclude group: 'org.yaml', module: 'snakeyaml'
}

dependencies {

    //    wearApp project(':twidere.wear')
    compile 'com.android.support:support-v13:21.0.3'
    compile 'com.android.support:cardview-v7:21.0.3'
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.android.support:palette-v7:21.0.3'
    compile 'com.sothree.slidinguppanel:library:2.0.4'
    compile 'it.sephiroth.android.library.imagezoom:imagezoom:2.1.1'
    compile 'com.twitter:twitter-text:1.10.2'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
    compile 'org.apache.httpcomponents:httpclient-android:4.3.5'
    compile 'org.apache.httpcomponents:httpmime:4.3.5'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.0.0'
    compile 'com.squareup:otto:1.3.5'
    compile 'dnsjava:dnsjava:2.1.6'
    compile 'com.commonsware.cwac:merge:1.1.1'
    googleCompile 'com.google.android.gms:play-services:6.5.87'
    fdroidCompile 'org.osmdroid:osmdroid-android:4.2'
    fdroidCompile 'org.slf4j:slf4j-simple:1.7.9'
    compile project(':twidere.component.nyan')
    compile project(':twidere.component.viewer.media')
    compile project(':SlidingMenu')
    compile project(':DragSortListView')
    compile project(':MenuComponent')
    compile project(':MessageBubbleView')
    compile fileTree(dir: 'libs/main', include: ['*.jar'])
    googleCompile fileTree(dir: 'libs/google', include: ['*.jar'])
    compile files('libs/google/YouTubeAndroidPlayerApi.jar')
}
